apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation project(':TMessagesProj')
    implementation 'androidx.multidex:multidex:2.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation files('../TMessagesProj/libs/libgsaverification-client.aar')
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'

    defaultConfig.applicationId = APP_PACKAGE

    sourceSets.main.jniLibs.srcDirs = ['../TMessagesProj/jni/']

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'BlockedPrivateApi'
    }

    dexOptions {
        jumboMode = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("$rootProject.projectDir/keys/debug.keystore")

            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ""
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel = 'SYMBOL_TABLE'
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../TMessagesProj/proguard-rules.pro'
            ndk.debugSymbolLevel = 'SYMBOL_TABLE'
        }
    }

    sourceSets.debug {
        manifest.srcFile '../TMessagesProj/config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile '../TMessagesProj/config/release/AndroidManifest.xml'
    }

    def props = new Properties()
    file("version.properties").withInputStream { props.load(it) }
    def majorVersion = Integer.valueOf(props.getProperty("major"))
    def minorVersion = Integer.valueOf(props.getProperty("minor"))
    def hotfixVersion = Integer.valueOf(props.getProperty("hotfix"))
    def calculatedVersionCode = (majorVersion * 100 + minorVersion) * 100 + hotfixVersion

    defaultConfig {
        compileSdk = 33
        buildToolsVersion = '33.0.0'
        minSdkVersion 28
        targetSdkVersion 33
        versionName "$majorVersion.$minorVersion.$hotfixVersion"
        versionCode = calculatedVersionCode
        vectorDrawables.generatedDensities = ['mdpi', 'hdpi']
        ndkVersion "21.1.6352462"

        ndk {
            abiFilters "armeabi-v7a"
        }

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-16', "-j=16"
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

apply plugin: 'com.google.gms.google-services'
